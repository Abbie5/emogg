plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

archivesBaseName = "${project.archives_base_name}-forge"

minecraft {
    mappings channel: 'official', version: minecraft_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory rootProject.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                emogg {
                    source sourceSets.main
                    source project(":xplat").sourceSets.main
                }
            }
        }

        client {}
        server {}
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly project(":xplat")

    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

mixin {
    add sourceSets.main, "emogg.refmap.json"
    config "emogg.mixins.json"
}

tasks.withType(JavaCompile).configureEach {
    source(project(":xplat").sourceSets.main.allSource)
}

processResources {
    from project(":xplat").sourceSets.main.resources
}

jar.finalizedBy('reobfJar')